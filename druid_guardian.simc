########################################

actions+=/skull_bash
actions+=/soothe
actions+=/call_action_list,name=mitigation

actions.mitigation+=/barkskin
actions.mitigation+=/ironfur,if=buff.bear_form.up&tanking&incoming_damage_5s>0.1*health.max&buff.ironfur.remains<1
actions.mitigation+=/regrowth,if=health.pct<65
actions.mitigation+=/renewal,if=health.pct<65
actions.mitigation+=/frenzied_regeneration,if=buff.bear_form.up&buff.frenzied_regeneration.down&health.pct<65
actions.mitigation+=/survival_instincts,if=buff.survival_instincts.down&buff.barkskin.down&health.pct<60&incoming_damage_5s>0.25*health.max

########################################

actions.bear+=/ironfur,if=in_combat&buff.ironfur.remains<1

actions.precombat=flask
actions.precombat+=/food
actions.precombat+=/augmentation

actions.precombat+=/snapshot_stats

actions=auto_attack,if=!buff.prowl.up

########################################

actions.precombat+=/bear_form,if=!(buff.bear_form.up|buff.cat_form.up&buff.wildshape_mastery.up)
actions.precombat+=/mark_of_the_wild
actions.precombat+=/variable,name=ironfur_spam,value=1,value_else=0,if=talent.thorns_of_iron.enabled&talent.reinforced_fur.enabled
actions.precombat+=/variable,name=ripweaving,value=1,value_else=0,if=talent.rip.enabled&talent.fluid_form.enabled&talent.wildpower_surge.enabled
actions.precombat+=/variable,name=use_cat_filler,value=1,value_else=0,if=variable.ripweaving&talent.primal_fury.enabled
actions.precombat+=/heart_of_the_wild,if=!variable.ripweaving


actions+=/use_item,slot=trinket1
actions+=/use_item,slot=trinket2
actions+=/potion,if=(buff.berserk_bear.up|buff.incarnation_guardian_of_ursoc.up)
actions+=/run_action_list,name=cat,if=buff.cat_form.up
actions+=/run_action_list,name=bear


actions.bear+=/moonfire,if=(!talent.galactic_guardian.enabled|talent.boundless_moonlight.enabled)&refreshable&target.time_to_die>9&spell_targets.swipe_bear<=3
actions.bear+=/thrash_bear,if=refreshable|dot.thrash_bear.stack<3+talent.flashing_claws.rank

actions.bear+=/pulverize
actions.bear+=/barkskin
actions.bear+=/bristling_fur,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>8)

actions.bear+=/heart_of_the_wild,if=!variable.ripweaving
actions.bear+=/convoke_the_spirits,if=!variable.ripweaving
actions.bear+=/incarnation
actions.bear+=/berserk_bear
actions.bear+=/lunar_beam,if=(cooldown.incarnation_guardian_of_ursoc.remains>10+5*talent.ursocs_guidance.enabled|cooldown.berserk_bear.remains>10)&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.ready|cooldown.rage_of_the_sleeper.remains>5)
actions.bear+=/rage_of_the_sleeper,if=(cooldown.incarnation_guardian_of_ursoc.remains>10+5*talent.ursocs_guidance.enabled|cooldown.berserk_bear.remains>10)&(!talent.lunar_beam.enabled|cooldown.lunar_beam.remains>5)&rage>40
actions.bear+=/berserking,if=buff.incarnation_guardian_of_ursoc.up|buff.berserk_bear.up

actions.bear+=/maul,if=buff.ravage.up&buff.tooth_and_claw.up&buff.vicious_cycle_maul.stack=3
actions.bear+=/maul,if=buff.ravage.up&buff.ravage.remains<2*gcd.max

actions.bear+=/maul,if=buff.tooth_and_claw.up&buff.tooth_and_claw.remains<2*gcd.max&buff.ravage.up
actions.bear+=/raze,if=buff.tooth_and_claw.up&buff.tooth_and_claw.remains<2*gcd.max&spell_targets.swipe_bear>=2
actions.bear+=/maul,if=buff.tooth_and_claw.up&buff.tooth_and_claw.remains<2*gcd.max&spell_targets.swipe_bear<=5

actions.bear+=/ironfur,if=!buff.ravage.up&variable.ironfur_spam&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)
actions.bear+=/ironfur,if=rage.deficit<=(12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled)<?(talent.blood_frenzy.enabled*3*(spell_targets.swipe_bear>?5))

actions.bear+=/thrash_bear,if=talent.lunar_calling.enabled&spell_targets.swipe_bear>=5&rage.deficit>talent.blood_frenzy.enabled*15

actions.bear+=/mangle,if=variable.ripweaving&spell_targets.swipe_bear<=3&buff.feline_potential_counter.stack<6&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.bear+=/maul,if=buff.ravage.up
actions.bear+=/call_action_list,name=preweave,if=variable.ripweaving&spell_targets.swipe_bear<=3&energy>60&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)&buff.feline_potential_counter.stack=6
actions.bear+=/call_action_list,name=builder,if=talent.ravage.enabled

actions.bear+=/raze,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2&!(talent.ravage.enabled&variable.ironfur_spam)
actions.bear+=/maul,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5&!(talent.ravage.enabled&variable.ironfur_spam)
actions.bear+=/raze,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2&(talent.ravage.enabled&variable.ironfur_spam)&buff.tooth_and_claw.stack=2
actions.bear+=/maul,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5&(talent.ravage.enabled&variable.ironfur_spam)&buff.tooth_and_claw.stack=2
actions.bear+=/raze,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear>=2&(talent.ravage.enabled&variable.ironfur_spam)&buff.tooth_and_claw.stack=1&buff.vicious_cycle_maul.stack>=1
actions.bear+=/maul,if=(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains>1)&spell_targets.swipe_bear<=5&(talent.ravage.enabled&variable.ironfur_spam)&buff.tooth_and_claw.stack=1&buff.vicious_cycle_maul.stack=3

actions.bear+=/call_action_list,name=builder,if=!talent.ravage.enabled

actions.bear+=/call_action_list,name=preweave,if=variable.use_cat_filler&spell_targets.swipe_bear<=3&energy>60&(!talent.rage_of_the_sleeper.enabled|cooldown.rage_of_the_sleeper.remains<=52)
actions.bear+=/starsurge,if=talent.starsurge.enabled&rage<20
actions.bear+=/moonfire,if=talent.lunar_insight.enabled&spell_targets.swipe_bear>=2&spell_targets.swipe_bear<=4
actions.bear+=/swipe_bear


actions.builder+=/mangle,if=buff.incarnation.up&spell_targets.swipe_bear>=2&spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.builder+=/thrash_bear,if=spell_targets.swipe_bear>=5&rage.deficit>talent.blood_frenzy.enabled*15
actions.builder+=/mangle,if=spell_targets.swipe_bear<=10&rage.deficit>12+4*buff.gore.up+5*talent.soul_of_the_forest.enabled
actions.builder+=/thrash_bear,if=spell_targets.swipe_bear>=1&spell_targets.swipe_bear<=4&rage.deficit>talent.blood_frenzy.enabled*3*spell_targets.swipe_bear
actions.builder+=/moonfire,if=buff.galactic_guardian.up&talent.boundless_moonlight.enabled&rage.deficit>8


actions.preweave+=/heart_of_the_wild,if=buff.feline_potential_counter.stack=6
actions.preweave+=/ironfur,if=!buff.ravage.up&variable.ironfur_spam&buff.feline_potential_counter.stack=6
actions.preweave+=/rake,target_if=max:(target.time_to_die>?60)-86400*(!refreshable),if=refreshable
actions.preweave+=/shred,if=!dot.rake.refreshable


actions.cat+=/mangle,if=buff.wildshape_mastery.remains<gcd.max
actions.cat+=/convoke_the_spirits,if=buff.feline_potential.up
actions.cat+=/rip,target_if=max:(target.time_to_die>?60)-86400*(!refreshable),if=buff.feline_potential.up&refreshable&target.time_to_die>12
actions.cat+=/ferocious_bite,if=buff.feline_potential.up&energy>50
actions.cat+=/rip,target_if=max:(target.time_to_die>?60)-86400*(ticking),if=buff.feline_potential.down&!ticking&combo_points>=2&combo_points<=7-buff.feline_potential_counter.stack
actions.cat+=/ferocious_bite,if=buff.feline_potential.down&energy>50&combo_points=5
actions.cat+=/mangle,if=buff.feline_potential.down
actions.cat+=/shred,if=energy>60
