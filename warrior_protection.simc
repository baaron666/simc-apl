########################################

actions+=/pummel
actions+=/call_action_list,name=mitigation

actions.mitigation+=/ignore_pain,use_off_gcd=1,if=tanking&buff.ignore_pain.down
actions.mitigation+=/spell_block,use_off_gcd=1,if=tanking&incoming_magic_damage_5s>spell_block_damage_taken&health.pct<=spell_block_health_pct&!(buff.spell_reflection_defense.up)
actions.mitigation+=/shield_wall,use_off_gcd=1,if=tanking&incoming_damage_5s>shield_wall_damage_taken&health.pct<=shield_wall_health_pct&!(buff.shield_wall.up|buff.last_stand.up)
actions.mitigation+=/last_stand,use_off_gcd=1,if=tanking&incoming_damage_5s>last_stand_damage_taken&health.pct<=last_stand_health_pct&!(buff.shield_wall.up|buff.last_stand.up)
actions.mitigation+=/rallying_cry,if=tanking&incoming_damage_5s>rallying_cry_damage_taken&health.pct<=rallying_cry_health_pct&!(buff.shield_wall.up|buff.last_stand.up)
actions.mitigation+=/victory_rush,if=health.pct<victory_rush_health_pct&talent.impending_victory.disabled
actions.mitigation+=/impending_victory,if=health.pct<victory_rush_health_pct&talent.impending_victory.enabled

########################################

actions.precombat=snapshot_stats
actions=auto_attack
# actions+=/ignore_pain,use_off_gcd=1,if=buff.ignore_pain.down&talent.one_against_many.enabled
# actions+=/ignore_pain,use_off_gcd=1,if=buff.ignore_pain.down&talent.demolish.enabled
actions+=/ignore_pain,use_off_gcd=1,if=buff.ignore_pain.remains<=0

########################################

actions.precombat+=/battle_stance,toggle=on
# actions.precombat+=/defensive_stance,toggle=on,if=talent.fight_through_the_flames.enabled
actions.precombat+=/battle_shout

actions+=/charge,if=time=0|movement.distance>8

actions+=/ignore_pain,use_off_gcd=1,if=cooldown.avatar.ready&rage.deficit<=10+10*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.ravager.remains<=gcd.remains&rage.deficit<=10+10*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.demoralizing_shout.remains<=gcd.remains&talent.booming_voice.enabled&rage.deficit<=10+20*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.champions_spear.remains<=gcd.remains&rage.deficit<=10+10*(1+talent.piercing_challenge.enabled)*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.shield_charge.remains<=gcd.remains&rage.deficit<=10+20*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.shield_slam.remains<=gcd.remains&(talent.lightning_strikes.enabled|talent.demolish.enabled&!((spell_targets.revenge>=4-(buff.colossal_might.stack>=6))))&rage.deficit<=10+(15+2*talent.heavy_repercussions.enabled+3*talent.practiced_strikes.enabled)*(1+0.5*buff.violent_outburst.up*(talent.demolish.enabled|talent.lightning_strikes.enabled&spell_targets.thunder_clap<5-buff.thunder_blast.up-buff.avatar.up*talent.unstoppable_force.enabled))*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)
actions+=/ignore_pain,use_off_gcd=1,if=cooldown.thunder_clap.remains<=gcd.remains&(talent.lightning_strikes.enabled|talent.demolish.enabled&!(!buff.violent_outburst.up&(cooldown.execute.remains<=gcd.remains|spell_targets.revenge>=3-(buff.colossal_might.stack>=6))|spell_targets.thunder_clap>=3&(spell_targets.revenge>=3-(buff.colossal_might.stack>=6))))&rage.deficit<=10+(5+(3+3*talent.thorims_might.enabled*talent.flashing_skies.enabled)*buff.thunder_blast.up)*(1+0.5*buff.violent_outburst.up*(talent.lightning_strikes.enabled&spell_targets.thunder_clap>=5-buff.thunder_blast.up-buff.avatar.up*talent.unstoppable_force.enabled))*(1+0.5*buff.last_stand.up*talent.unnerving_focus.enabled)

actions+=/avatar
actions+=/use_items
actions+=/potion,if=buff.avatar.up
actions+=/blood_fury
actions+=/berserking
actions+=/arcane_torrent
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks

actions+=/ravager
actions+=/demoralizing_shout,if=talent.booming_voice.enabled
actions+=/champions_spear
actions+=/demolish,if=talent.sudden_death.enabled&buff.colossal_might.stack>=7
actions+=/demolish,if=!talent.sudden_death.enabled&buff.colossal_might.stack>=3
actions+=/thunderous_roar
actions+=/shield_charge
actions+=/shield_block,use_off_gcd=1,if=buff.shield_block.remains<=2

actions+=/run_action_list,name=colossus,if=talent.demolish.enabled
actions+=/run_action_list,name=thane,if=talent.lightning_strikes.enabled


actions.colossus+=/revenge,if=spell_targets.revenge>=4-(buff.colossal_might.stack>=6)
actions.colossus+=/shield_slam
actions.colossus+=/revenge,if=spell_targets.revenge>=3-(buff.colossal_might.stack>=6)
actions.colossus+=/thunder_clap,if=spell_targets.thunder_clap>=3
actions.colossus+=/execute
actions.colossus+=/revenge,if=buff.colossal_might.stack>=10
actions.colossus+=/thunder_clap,if=!buff.violent_outburst.up
actions.colossus+=/revenge


actions.thane+=/execute,if=talent.juggernaut.enabled
actions.thane+=/thunder_blast,if=!buff.violent_outburst.up&spell_targets.thunder_clap<=1&buff.thunder_blast.stack=2
actions.thane+=/thunder_blast,if=spell_targets.thunder_clap>=5-buff.thunder_blast.up-buff.avatar.up*talent.unstoppable_force.enabled
actions.thane+=/thunder_clap,if=spell_targets.thunder_clap>=5-buff.thunder_blast.up-buff.avatar.up*talent.unstoppable_force.enabled
actions.thane+=/shield_slam,if=!(buff.violent_outburst.up&spell_targets.thunder_clap>=5-buff.thunder_blast.up-buff.avatar.up*talent.unstoppable_force.enabled)
actions.thane+=/revenge,if=spell_targets.revenge>=5
actions.thane+=/execute
actions.thane+=/thunder_blast,if=!buff.violent_outburst.up
actions.thane+=/thunder_clap,if=!buff.violent_outburst.up
actions.thane+=/revenge
